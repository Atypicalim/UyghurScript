# calculator

miqdar ENG_CHONG_KIRGUZGILI_BULDIGHAN_QIMMET qimmiti 999 bolsun
miqdar ENG_CHONG_HISABLIGHILI_BULDIGHAN_QIMMET qimmiti 999999 bolsun

miqdar herketQimmet qimmiti quruq bolsun
miqdar sinaqXet qimmiti quruq bolsun

miqdar kunupkaNumuri qimmiti quruq bolsun

miqdar kunupkaOrniX qimmiti quruq bolsun
miqdar kunupkaOrniY qimmiti quruq bolsun
miqdar kordinatX qimmiti quruq bolsun
miqdar kordinatY qimmiti quruq bolsun
miqdar kunupkaXeti qimmiti quruq bolsun

miqdar netijeQimmet qimmiti 0 bolsun
miqdar hazirqiQimmet qimmiti 0 bolsun
miqdar aldinqiQimmet qimmiti 15 bolsun

miqdar kunupkaSani qimmiti 16 bolsun
miqdar tekshuruldighanQimmet qimmiti yalghan bolsun

miqdar test1 qimmiti quruq bolsun
miqdar test2 qimmiti quruq bolsun
miqdar aldinqisiSanmu qimmiti quruq bolsun
miqdar aldinqisiHerketmu qimmiti quruq bolsun
miqdar aldinqisiTengmu qimmiti quruq bolsun
miqdar buSanmu qimmiti quruq bolsun
miqdar buHerketmu qimmiti quruq bolsun
miqdar buTengmu qimmiti quruq bolsun

miqdar sinaqQimmet qimmiti quruq bolsun

# get sign
fonkisiye belgilerniElish miqdar x mezmuni
    miqdar r qimmiti quruq bolsun
    eger x ? quruq bolsa
        r qimmiti [] bolsun
    egerde x ? 1 bolsa
        r qimmiti [1] bolsun
    egerde x ? 2 bolsa
        r qimmiti [2] bolsun
    egerde x ? 3 bolsa
        r qimmiti [3] bolsun
    egerde x ? 4 bolsa
        r qimmiti [4] bolsun
    egerde x ? 5 bolsa
        r qimmiti [5] bolsun
    egerde x ? 6 bolsa
        r qimmiti [6] bolsun
    egerde x ? 7 bolsa
        r qimmiti [7] bolsun
    egerde x ? 8 bolsa
        r qimmiti [8] bolsun
    egerde x ? 9 bolsa
        r qimmiti [9] bolsun
    egerde x ? 10 bolsa
        r qimmiti [0] bolsun
    egerde x ? 11 bolsa
        r qimmiti [+] bolsun
    egerde x ? 12 bolsa
        r qimmiti [-] bolsun
    egerde x ? 13 bolsa
        r qimmiti [*] bolsun
    egerde x ? 14 bolsa
        r qimmiti [/] bolsun
    egerde x ? 15 bolsa
        r qimmiti [=] bolsun
    bolmisa
        fonkisiye @xet[formatlash] bilen [%f] x ishlitilsun we netije r bolsun # string_format bug
    tamamlansun
    netije r qayturulsun
tamamlansun

# reset button
fonkisiye tazilashQayturmisi mezmuni
    ikrangha [tazilandi] yezilsun
    netijeQimmet qimmiti 0 bolsun
    hazirqiQimmet qimmiti 0 bolsun
    aldinqiQimmet qimmiti 15 bolsun
    herketQimmet qimmiti quruq bolsun
tamamlansun

# calculate result
fonkisiye netijiniHisablash mezmuni
    eger herketQimmet ? quruq bolsa
        netijeQimmet qimmiti hazirqiQimmet bolsun
    egerde herketQimmet ? 11 bolsa
        netijeQimmet = netijeQimmet + hazirqiQimmet
    egerde herketQimmet ? 12 bolsa
        netijeQimmet = netijeQimmet - hazirqiQimmet
    egerde herketQimmet ? 13 bolsa
        netijeQimmet = netijeQimmet * hazirqiQimmet
    egerde herketQimmet ? 14 bolsa
        netijeQimmet = netijeQimmet / hazirqiQimmet
    tamamlansun
    herketQimmet qimmiti quruq bolsun
    hazirqiQimmet qimmiti 0 bolsun
tamamlansun

# buttons callback
fonkisiye kunupkaQauturmisi miqdar id mezmuni
    # check
    test1 = aldinqiQimmet > 0
    test2 = aldinqiQimmet < 11
    aldinqisiSanmu = test1 & test2
    test1 = aldinqiQimmet > 10
    test2 = aldinqiQimmet < 15
    aldinqisiHerketmu = test1 & test2
    aldinqisiTengmu = aldinqiQimmet ? 15
    # check
    test1 = id > 0
    test2 = id < 11
    buSanmu = test1 & test2
    test1 = id > 10
    test2 = id < 15
    buHerketmu = test1 & test2
    buTengmu = id ? 15
    # invalid
    sinaqQimmet = buTengmu & aldinqisiTengmu
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    sinaqQimmet = buTengmu & aldinqisiHerketmu
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    sinaqQimmet = buHerketmu & aldinqisiHerketmu
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    sinaqQimmet = buHerketmu & aldinqisiTengmu
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    sinaqQimmet = aldinqiQimmet > ENG_CHONG_KIRGUZGILI_BULDIGHAN_QIMMET
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    sinaqQimmet = netijeQimmet > ENG_CHONG_HISABLIGHILI_BULDIGHAN_QIMMET
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    # record
    aldinqiQimmet qimmiti id bolsun
    # input number
    eger buSanmu bolsa
        hazirqiQimmet = hazirqiQimmet * 10
        hazirqiQimmet = hazirqiQimmet + id
        eger id ? 10 bolsa
            hazirqiQimmet = hazirqiQimmet - 10
        tamamlansun
        eger aldinqisiTengmu bolsa
            netijeQimmet qimmiti 0 bolsun
        tamamlansun
        netije quruq qayturulsun
    tamamlansun
    # input action
    eger buHerketmu bolsa
        fonkisiye netijiniHisablash ishlitilsun
        hazirqiQimmet qimmiti 0 bolsun
        herketQimmet qimmiti id bolsun
    tamamlansun
    # equal button
    eger buTengmu bolsa
        fonkisiye netijiniHisablash ishlitilsun
        hazirqiQimmet qimmiti 0 bolsun
        herketQimmet qimmiti quruq bolsun
    tamamlansun
tamamlansun

# draw button
fonkisiye kunupkaSizish miqdar id mezmuni
    # 2d point
    fonkisiye @san[qalduqniHisablash] bilen id 5 ishlitilsun we netije kordinatX bolsun
    fonkisiye @san[bulunminiHisablash] bilen id 5 ishlitilsun we netije kordinatY bolsun
    eger kordinatX ? 0 bolsa
        kordinatX qimmiti 5 bolsun
        kordinatY = kordinatY - 1
    tamamlansun
    # position
    kordinatX = kordinatX - 1
    kordinatY = kordinatY + 1
    kunupkaOrniX = 101 * kordinatX
    kunupkaOrniY = 51 * kordinatY
    kunupkaOrniX = kunupkaOrniX + 5
    kunupkaOrniY = kunupkaOrniY + 15
    fonkisiye belgilerniElish bilen id ishlitilsun we netije kunupkaXeti bolsun
    # button
    fonkisiye @zapchas[kunupkaKorsitish] bilen kunupkaOrniX kunupkaOrniY 100 50 kunupkaXeti ishlitilsun we netije rr bolsun
    eger rr bolsa
        fonkisiye kunupkaQauturmisi bilen id ishlitilsun
    tamamlansun

tamamlansun

fonkisiye @*[doska_sizish_qayturmisi] mezmuni

    # buttons
    kunupkaNumuri qimmiti 1 bolsun
    tekshuruldighanQimmet = kunupkaNumuri < kunupkaSani
    nawada tekshuruldighanQimmet bolsa
        fonkisiye kunupkaSizish bilen kunupkaNumuri ishlitilsun
        kunupkaNumuri = kunupkaNumuri + 1
        tekshuruldighanQimmet = kunupkaNumuri < kunupkaSani
    tamamlansun
    # screen
    fonkisiye @ressam[rayunToldurushAylandurulghan] bilen 7 5 500 55 [555555] 0 0 0 ishlitilsun
    fonkisiye @ressam[rayunToldurushAylandurulghan] bilen 12 10 333 45 [333333] 0 0 0 ishlitilsun
    fonkisiye @ressam[rayunToldurushAylandurulghan] bilen 350 20 70 35 [88888833] 0 0 0 ishlitilsun
    # clear
    fonkisiye @zapchas[kunupkaKorsitish] bilen 425 10 75 45 [Tazilash] ishlitilsun we netije rr bolsun
    eger rr bolsa
        fonkisiye tazilashQayturmisi ishlitilsun
    tamamlansun
    # input
    fonkisiye belgilerniElish bilen herketQimmet ishlitilsun we netije sinaqXet bolsun
    fonkisiye @ressam[xetYezish] bilen [] sinaqXet 36 2 [111111] 377 21 ishlitilsun
    # result
    fonkisiye @xet[formatlash] bilen [%f] netijeQimmet ishlitilsun we netije sinaqXet bolsun
    fonkisiye @ressam[xetYezish] bilen [] sinaqXet 24 2 [aaaaaa] 25 15 ishlitilsun
    # action
    fonkisiye @xet[formatlash] bilen [%f] hazirqiQimmet ishlitilsun we netije sinaqXet bolsun
    fonkisiye @ressam[xetYezish] bilen [] sinaqXet 16 2 [aaaaaa] 25 38 ishlitilsun
tamamlansun

fonkisiye @doska[korsitish] bilen 515 225 [Xisabliguch] 4096 ishlitilsun
