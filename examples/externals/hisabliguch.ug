# calculator

miqdar ENG_CHONG_KIRGUZGILI_BULDIGHAN_QIMMET qimmiti 999 bolsun
miqdar ENG_CHONG_HISABLIGHILI_BULDIGHAN_QIMMET qimmiti 999999 bolsun

miqdar herketQimmet qimmiti quruq bolsun
miqdar sinaqXet qimmiti quruq bolsun

miqdar kunupkaNumuri qimmiti quruq bolsun

miqdar kunupkaOrniX qimmiti quruq bolsun
miqdar kunupkaOrniY qimmiti quruq bolsun
miqdar kordinatX qimmiti quruq bolsun
miqdar kordinatY qimmiti quruq bolsun
miqdar kunupkaXeti qimmiti quruq bolsun

miqdar netijeQimmet qimmiti 0 bolsun
miqdar hazirqiQimmet qimmiti 0 bolsun
miqdar aldinqiQimmet qimmiti 15 bolsun

miqdar kunupkaSani qimmiti 16 bolsun
miqdar tekshuruldighanQimmet qimmiti yalghan bolsun

miqdar test1 qimmiti quruq bolsun
miqdar test2 qimmiti quruq bolsun
miqdar aldinqisiSanmu qimmiti quruq bolsun
miqdar aldinqisiHerketmu qimmiti quruq bolsun
miqdar aldinqisiTengmu qimmiti quruq bolsun
miqdar buSanmu qimmiti quruq bolsun
miqdar buHerketmu qimmiti quruq bolsun
miqdar buTengmu qimmiti quruq bolsun

miqdar sinaqQimmet qimmiti quruq bolsun

# get sign
xizmetchi belgilerniElish miqdar x mezmuni
    miqdar r qimmiti quruq bolsun
    eger x ? quruq bolsa
        r qimmiti "" bolsun
    egerde x ? 1 bolsa
        r qimmiti "1" bolsun
    egerde x ? 2 bolsa
        r qimmiti "2" bolsun
    egerde x ? 3 bolsa
        r qimmiti "3" bolsun
    egerde x ? 4 bolsa
        r qimmiti "4" bolsun
    egerde x ? 5 bolsa
        r qimmiti "5" bolsun
    egerde x ? 6 bolsa
        r qimmiti "6" bolsun
    egerde x ? 7 bolsa
        r qimmiti "7" bolsun
    egerde x ? 8 bolsa
        r qimmiti "8" bolsun
    egerde x ? 9 bolsa
        r qimmiti "9" bolsun
    egerde x ? 10 bolsa
        r qimmiti "0" bolsun
    egerde x ? 11 bolsa
        r qimmiti "+" bolsun
    egerde x ? 12 bolsa
        r qimmiti "-" bolsun
    egerde x ? 13 bolsa
        r qimmiti "*" bolsun
    egerde x ? 14 bolsa
        r qimmiti "/" bolsun
    egerde x ? 15 bolsa
        r qimmiti "=" bolsun
    bolmisa
        xizmetchi @xet:formatlash bilen "%f" x ishlitilsun we r elinsun # string_format bug
    tamamlansun
    netije r qayturulsun
tamamlansun

# reset button
xizmetchi tazilashQayturmisi mezmuni
    buyruq "tazilandi" yezilsun
    netijeQimmet qimmiti 0 bolsun
    hazirqiQimmet qimmiti 0 bolsun
    aldinqiQimmet qimmiti 15 bolsun
    herketQimmet qimmiti quruq bolsun
tamamlansun

# calculate result
xizmetchi netijiniHisablash mezmuni
    eger herketQimmet ? quruq bolsa
        netijeQimmet qimmiti hazirqiQimmet bolsun
    egerde herketQimmet ? 11 bolsa
        netijeQimmet = netijeQimmet + hazirqiQimmet
    egerde herketQimmet ? 12 bolsa
        netijeQimmet = netijeQimmet - hazirqiQimmet
    egerde herketQimmet ? 13 bolsa
        netijeQimmet = netijeQimmet * hazirqiQimmet
    egerde herketQimmet ? 14 bolsa
        netijeQimmet = netijeQimmet / hazirqiQimmet
    tamamlansun
    herketQimmet qimmiti quruq bolsun
    hazirqiQimmet qimmiti 0 bolsun
tamamlansun

# buttons callback
xizmetchi kunupkaQauturmisi miqdar id mezmuni
    # check
    test1 = aldinqiQimmet > 0
    test2 = aldinqiQimmet < 11
    aldinqisiSanmu = test1 & test2
    test1 = aldinqiQimmet > 10
    test2 = aldinqiQimmet < 15
    aldinqisiHerketmu = test1 & test2
    aldinqisiTengmu = aldinqiQimmet ? 15
    # check
    test1 = id > 0
    test2 = id < 11
    buSanmu = test1 & test2
    test1 = id > 10
    test2 = id < 15
    buHerketmu = test1 & test2
    buTengmu = id ? 15
    # invalid
    sinaqQimmet = buTengmu & aldinqisiTengmu
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    sinaqQimmet = buTengmu & aldinqisiHerketmu
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    sinaqQimmet = buHerketmu & aldinqisiHerketmu
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    sinaqQimmet = buHerketmu & aldinqisiTengmu
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    sinaqQimmet = aldinqiQimmet > ENG_CHONG_KIRGUZGILI_BULDIGHAN_QIMMET
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    sinaqQimmet = netijeQimmet > ENG_CHONG_HISABLIGHILI_BULDIGHAN_QIMMET
    eger sinaqQimmet bolsa
        netije quruq qayturulsun
    tamamlansun
    # record
    aldinqiQimmet qimmiti id bolsun
    # input number
    eger buSanmu bolsa
        hazirqiQimmet = hazirqiQimmet * 10
        hazirqiQimmet = hazirqiQimmet + id
        eger id ? 10 bolsa
            hazirqiQimmet = hazirqiQimmet - 10
        tamamlansun
        eger aldinqisiTengmu bolsa
            netijeQimmet qimmiti 0 bolsun
        tamamlansun
        netije quruq qayturulsun
    tamamlansun
    # input action
    eger buHerketmu bolsa
        xizmetchi netijiniHisablash ishlitilsun
        hazirqiQimmet qimmiti 0 bolsun
        herketQimmet qimmiti id bolsun
    tamamlansun
    # equal button
    eger buTengmu bolsa
        xizmetchi netijiniHisablash ishlitilsun
        hazirqiQimmet qimmiti 0 bolsun
        herketQimmet qimmiti quruq bolsun
    tamamlansun
tamamlansun

# draw button
xizmetchi kunupkaSizish miqdar id mezmuni
    # 2d point
    xizmetchi @san:qalduqniHisablash bilen id 5 ishlitilsun we kordinatX elinsun
    xizmetchi @san:bulunminiHisablash bilen id 5 ishlitilsun we kordinatY elinsun
    eger kordinatX ? 0 bolsa
        kordinatX qimmiti 5 bolsun
        kordinatY = kordinatY - 1
    tamamlansun
    # position
    kordinatX = kordinatX - 1
    kordinatY = kordinatY + 1
    kunupkaOrniX = 101 * kordinatX
    kunupkaOrniY = 51 * kordinatY
    kunupkaOrniX = kunupkaOrniX + 5
    kunupkaOrniY = kunupkaOrniY + 15
    xizmetchi belgilerniElish bilen id ishlitilsun we kunupkaXeti elinsun
    # button
    xizmetchi @zapchas:kunupkaKorsitish bilen kunupkaOrniX kunupkaOrniY 100 50 kunupkaXeti ishlitilsun we rr elinsun
    eger rr bolsa
        xizmetchi kunupkaQauturmisi bilen id ishlitilsun
    tamamlansun

tamamlansun

xizmetchi @sehne:korsitish bilen 515 225 "Xisabliguch" -1 ishlitilsun
miqdar dawam qimmiti rast bolsun
nawada dawam bolsa
    xizmetchi @taxta:sizishniBashlash ishlitilsun
    #
    # buttons
    kunupkaNumuri qimmiti 1 bolsun
    tekshuruldighanQimmet = kunupkaNumuri < kunupkaSani
    nawada tekshuruldighanQimmet bolsa
        xizmetchi kunupkaSizish bilen kunupkaNumuri ishlitilsun
        kunupkaNumuri = kunupkaNumuri + 1
        tekshuruldighanQimmet = kunupkaNumuri < kunupkaSani
    tamamlansun
    # screen
    xizmetchi @taxta:totTereplikToldurushAylandurulghan bilen 7 5 500 55 "555555" 0 0 0 ishlitilsun
    xizmetchi @taxta:totTereplikToldurushAylandurulghan bilen 12 10 333 45 "333333" 0 0 0 ishlitilsun
    xizmetchi @taxta:totTereplikToldurushAylandurulghan bilen 350 20 70 35 "88888833" 0 0 0 ishlitilsun
    # # clear
    xizmetchi @zapchas:kunupkaKorsitish bilen 425 10 75 45 "Tazilash" ishlitilsun we rr elinsun
    eger rr bolsa
        xizmetchi tazilashQayturmisi ishlitilsun
    tamamlansun
    # input
    xizmetchi belgilerniElish bilen herketQimmet ishlitilsun we sinaqXet elinsun
    xizmetchi @taxta:xetYezish bilen "" sinaqXet 36 2 "111111" 377 21 ishlitilsun
    # result
    xizmetchi @xet:formatlash bilen "%f" netijeQimmet ishlitilsun we sinaqXet elinsun
    xizmetchi @taxta:xetYezish bilen "" sinaqXet 24 2 "aaaaaa" 25 15 ishlitilsun
    # action
    xizmetchi @xet:formatlash bilen "%f" hazirqiQimmet ishlitilsun we sinaqXet elinsun
    xizmetchi @taxta:xetYezish bilen "" sinaqXet 16 2 "aaaaaa" 25 38 ishlitilsun
    #
    xizmetchi @taxta:sizishniTamamlash ishlitilsun
    xizmetchi @sehne:dawammu ishlitilsun we dawam elinsun
tamamlansun
xizmetchi @sehne:yushurush ishlitilsun
